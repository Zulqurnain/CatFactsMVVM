import groovy.json.JsonSlurper

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'
apply plugin: 'dagger.hilt.android.plugin'


def appVersionCode = 1
def appVersionName = "1.0.1"

def stagingVersionSuffix = "Build-1"

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation" : "$projectDir/schemas".toString(),
                        "room.incremental" : "true",
                ]
            }
        }
        applicationId "com.jutt.catfactsfeeddemo"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode appVersionCode
        versionName appVersionName
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true

        resConfigs "en", "ur"

        buildConfigField("String", "APPLICATION_FILE_PROVIDER", 'APPLICATION_ID + ".FileProvider"')

        def mobileAppScheme = "catfactsdemo"
        buildConfigField("String", "MOBILE_APP_SCHEME", '"' + mobileAppScheme + '"')
        buildConfigField("String", "APL", '"https://play.google.com/store/apps/details?id=' + applicationId + '"')
        manifestPlaceholders = [
                mobileAppScheme      : mobileAppScheme,
        ]
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        internalConfig {
            def credsFilePath = file("signings/credentials_internal.json")
            def credsFile = new File(credsFilePath.toString(), "").getText('UTF-8')
            def json = new JsonSlurper().parseText(credsFile)
            storeFile file(json.android.storeFile)
            storePassword = json.android.storePassword
            keyAlias = json.android.keyAlias
            keyPassword = json.android.keyPassword
        }
        storeConfig {
            def credsFilePath = file("signings/credentials_store.json")
            def credsFile = new File(credsFilePath, "").getText('UTF-8')
            def json = new JsonSlurper().parseText(credsFile)
            storeFile file(json.android.storeFile)
            storePassword = json.android.storePassword
            keyAlias = json.android.keyAlias
            keyPassword = json.android.keyPassword
        }
    }

    flavorDimensions "environment"
    buildTypes {
        debug {
            debuggable true
            minifyEnabled true
            shrinkResources true
            versionNameSuffix "-debug (${appVersionCode})"
            applicationIdSuffix ".internal"
        }
        internal {
            debuggable true
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile(
                    'proguard-android-optimize.txt'),
                    'proguard-rules.pro'
            matchingFallbacks = ['debug']
            signingConfig signingConfigs.internalConfig
            versionNameSuffix " (${stagingVersionSuffix})"
            applicationIdSuffix ".internal"
        }
        release {
            debuggable false
            minifyEnabled false
            shrinkResources false
            matchingFallbacks = ['debug']
            signingConfig signingConfigs.storeConfig
//            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
        staging {
            def scheme = "https"
            def host = "cat-fact.herokuapp.com"
            dimension "environment"
            versionNameSuffix "-staging"
            buildConfigField("String", "API_HOST_SCHEME", "\"${scheme}\"")
            buildConfigField("String", "BASE_URL", "\"${scheme}" + "://" + "${host}" + "/" + "\"")
            buildConfigField("String", "API_BASE_URL", "\"${scheme}" + "://" + "${host}" + "/" + "\"")
            manifestPlaceholders.put("host", host)
            manifestPlaceholders.put("usesCleartextTraffic", true)
        }
        live {
            def scheme = "https"
            def host = "cat-fact.herokuapp.com"
            versionNameSuffix "-live"
            buildConfigField("String", "API_HOST_SCHEME", "\"${scheme}\"")
            buildConfigField("String", "BASE_URL", "\"${scheme}" + "://" + "${host}" + "/" + "\"")
            buildConfigField("String", "API_BASE_URL", "\"${scheme}" + "://" + "${host}" + "/" + "\"")
            manifestPlaceholders.put("host", host)
            manifestPlaceholders.put("usesCleartextTraffic", true)
        }
    }
    /**
     * To skip assign true
     */
    def variantFilterSkipMap = [
            stagingDebug          : false,
            stagingRelease        : true,
            liveDebug             : true,
            liveRelease           : false,
            liveInternal          : true,
            stagingInternal       : false
    ]
    variantFilter { variant ->
        setIgnore(variantFilterSkipMap[variant.name] ?: false)
    }
    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "FactsV${appVersionName}.${appVersionCode}_${variant.name.capitalize()}.apk"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
    kapt {
        correctErrorTypes true
    }
    buildFeatures {
        viewBinding true
    }
    testOptions {
        unitTests.includeAndroidResources = true
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    kapt "androidx.room:room-compiler:$rootProject.roomVersion"
    implementation "androidx.appcompat:appcompat:$rootProject.appCompatVersion"
    implementation "androidx.cardview:cardview:$rootProject.cardViewVersion"
    implementation "androidx.constraintlayout:constraintlayout:$rootProject.constraintLayoutVersion"
    implementation "androidx.core:core-ktx:$rootProject.ktxVersion"
    implementation "androidx.fragment:fragment-ktx:$rootProject.fragmentVersion"
    implementation "androidx.multidex:multidex:$rootProject.multidexVersion"
    implementation "androidx.recyclerview:recyclerview:$rootProject.recyclerViewVersion"
    implementation "androidx.room:room-ktx:$rootProject.roomVersion"
    implementation "androidx.room:room-runtime:$rootProject.roomVersion"
    implementation "androidx.vectordrawable:vectordrawable-animated:$rootProject.vectorDrawableVersion"
    implementation "com.github.bumptech.glide:glide:$rootProject.glideVersion"
    implementation "com.google.android.material:material:$rootProject.materialDesignVersion"
    implementation "com.google.code.gson:gson:$rootProject.gsonVersion"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$rootProject.kotlinVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$rootProject.kotlinVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$rootProject.okHttpLoggingInterceptorVersion"
    implementation "com.squareup.retrofit2:converter-gson:$rootProject.retrofitVersion"
    implementation "com.squareup.retrofit2:retrofit:$rootProject.retrofitVersion"

    //Dagger - Hilt
    implementation "com.google.dagger:hilt-android:$rootProject.daggerHiltVersion"
    kapt "com.google.dagger:hilt-android-compiler:$rootProject.daggerHiltVersion"
    implementation "androidx.hilt:hilt-lifecycle-viewmodel:$rootProject.hiltLifeCycleVersion"
    kapt "androidx.hilt:hilt-compiler:$rootProject.hiltLifeCycleVersion"

    // Activity KTX for viewModels()
    implementation "androidx.activity:activity-ktx:$rootProject.activityKtx"

    implementation "com.intuit.sdp:sdp-android:$rootProject.androidSDP"
    implementation "com.intuit.ssp:ssp-android:$rootProject.androidSSP"

    implementation "com.xwray:groupie:$rootProject.groupie_version"
    implementation "com.xwray:groupie-viewbinding:$rootProject.groupie_version"

    implementation 'com.blankj:utilcodex:1.30.5'

    testImplementation 'junit:junit:4.13.2'
    testImplementation "com.google.truth:truth:1.0.1"

    androidTestImplementation "com.google.truth:truth:1.0.1"
    androidTestImplementation 'androidx.test.ext:junit-ktx:1.1.2'
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.2.1"
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'com.android.support:support-annotations:28.0.0'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'androidx.fragment:fragment-testing:1.3.0'
    androidTestImplementation 'androidx.test:core-ktx:1.3.0'
    androidTestImplementation 'org.mockito:mockito-core:2.19.0'

    testImplementation 'org.mockito:mockito-core:2.19.0'
}